Expression = { SOI ~ Relation ~ EOI }
Relation = { Addition ~ (RelOp ~ Addition)? }
RelOp = { "==" }
Addition = { Multiplication ~ (AddOp ~ Addition)? }
AddOp = { "+" | "-" }
Multiplication = { Unary ~ (MulOp ~ Multiplication)? }
MulOp = { "*" | "/" }
Unary = { Member | UnaryOp ~ Unary }
UnaryOp = { "-" | "!" }
Member = { (Literal | LPAREN ~ Relation ~ RPAREN) ~ (DOT ~ Identifier ~ Args)* }
LPAREN = _{ "(" }
RPAREN = _{ ")" }
DOT = _{ "." }
Literal = { StringLiteral | IntLiteral | ListLiteral | BoolLiteral }
StringLiteral = ${ PUSH(OPEN_STR) ~ (CharLiteral | Escape)* ~ POP }
OPEN_STR = _{ "\"" | "'" }
CharLiteral = { ASCII_ALPHANUMERIC }
Escape = @{ "\\" ~ ("\"" | "n" | "t" | OctalSequence | HexSequence | UnicodeSequence ) }
OctalSequence = { ('0' .. '3') ~ ASCII_OCT_DIGIT{2} }
HexSequence = { "x" ~ ASCII_HEX_DIGIT{2} }
UnicodeSequence = { "u" ~ ASCII_HEX_DIGIT{4} }
IntLiteral = @{ ASCII_DIGIT+ }
ListLiteral = { "[" ~ Addition ~ ("," ~ Addition)* ~ "]" }
BoolLiteral = { "false" | "true" }
Identifier = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
Args = { "(" ~ (Relation ~ ",")* ~ Relation? ~ ")" }
WHITESPACE = _{ " " | "\n" }
