Expression = { SOI ~ Relation ~ EOI }
Relation = { Addition ~ (RelOp ~ Addition)? }
RelOp = { "==" }
Addition = { Multiplication ~ (AddOp ~ Addition)? }
AddOp = { "+" | "-" }
Multiplication = { Unary ~ (MulOp ~ Multiplication)? }
MulOp = { "*" | "/" }
Unary = { Literal | LPAREN ~ Relation ~ RPAREN | UnaryOp ~ Unary }
LPAREN = _{ "(" }
RPAREN = _{ ")" }
UnaryOp = { "-" | "!" }
Literal = { StringLiteral | IntLiteral | ListLiteral | BoolLiteral }
StringLiteral = ${ PUSH(OPEN_STR) ~ (CharLiteral | Escape)* ~ POP }
OPEN_STR = _{ "\"" | "'" }
CharLiteral = { ASCII_ALPHANUMERIC }
Escape = @{ "\\" ~ ("\"" | "n" | "t" | Octal ) }
Octal = { ASCII_OCT_DIGIT }
IntLiteral = @{ ASCII_DIGIT+ }
ListLiteral = { "[" ~ Addition ~ ("," ~ Addition)* ~ "]" }
BoolLiteral = { "false" | "true" }
WHITESPACE = _{ " " | "\n" }
